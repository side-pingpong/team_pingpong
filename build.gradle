plugins {
    // 하위 모듈에서 버전을 명시하지 않아도 되도록 중앙에서 버전만 관리합니다.
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

// 모든 프로젝트(루트 포함)에 적용될 설정
allprojects {
    group = 'com.team_pingpong'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

// 모든 하위 프로젝트들을 대상으로...
subprojects {
    // 만약 그 프로젝트가 'java' 플러그인을 가지고 있다면, 아래 설정을 적용한다.
    plugins.withId('java') {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }

        // 롬복(Lombok)처럼 모든 자바 프로젝트에 공통으로 들어갈 의존성
        dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }
    }
}